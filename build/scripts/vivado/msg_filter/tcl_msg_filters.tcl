# tcl_msg_filters.tcl: definition of procedures for filtering Vivado messages
#                      in command line mode (according to UNITERESTING and
#                      MOST_INTERESTING lists of message IDs)
# Copyright (C) 2014 CESNET
# Author: Jiri Matousek <xmatou06@stud.fit.vutbr.cz>
#
# SPDX-License-Identifier: BSD-3-Clause
#
# $Id$
#


# load procedures defined in build/Vivado.inc.tcl


# -----------------------------------------------------------------------------
# filter_msg
# This procedure parses all log files generated by the Vivado and extracts all
# messages from these files. The messages and their subsets (all messages
# except INFOs, all messages without those defined in the UNINTERESTING list,
# only the messages defined in the MOST_INTERESTING list) are then wiritten to
# four separate files.
#
proc filter_msg { } {

   global OFM_PATH

   # import UNINTERESTING and MOST_INTERESTING lists
   source $OFM_PATH/build/scripts/vivado/msg_filter/msg_filtering_rules.tcl

   # get a list of all log files
   set LOGFILES [get_all_log_files]

   # get a list of all messages from all log files
   set MSG_LINES [list ]
   foreach FILE $LOGFILES {
      set MSG_LINES [concat $MSG_LINES [get_all_msg_lines $FILE]]
   }

   # open a file for all messages
   set MESSAGES_ALL [open "messages-all.txt" "w+"]

   # write all messages to the corresponding file
   # also create a new list of all messages except INFOs
   set MSG_LINES_NO_INFO [list ]
   foreach LINE $MSG_LINES {
      puts $MESSAGES_ALL $LINE
      if { ![regexp "INFO: " $LINE] } {
         lappend MSG_LINES_NO_INFO $LINE
      }
   }

   # close the file for all messages
   close $MESSAGES_ALL

   # open a file for all messages except INFOs
   set MESSAGES_NO_INFO [open "messages-no_info.txt" "w+"]

   # write all messages except INFOs to the corresponding file
   # also filter out all messages defined in the UNINTERESTING list and assign
   # remaning messages into a new list
   set MSG_LINES_NO_UNINTERESTING [list ]
   foreach LINE $MSG_LINES_NO_INFO {
      puts $MESSAGES_NO_INFO $LINE
      set IS_UNINTERESTING 0
      foreach MSG $UNINTERESTING {
         if { [regexp $MSG $LINE] } {
            set IS_UNINTERESTING 1
            break
         }
      }
      if { !$IS_UNINTERESTING } {
         lappend MSG_LINES_NO_UNINTERESTING $LINE
      }
   }

   # close the file for all messages except INFOs
   close $MESSAGES_NO_INFO

   # open a file for all messages not defined in the UNINTERESTING list
   set MESSAGES_NO_UNINTERESTING [open "messages-no_uninteresting.txt" "w+"]

   # write all messages except those defined as uniteresting to the
   # corresponding file
   # also extract all messages definec in the MOST_INTERESTING list
   set MSG_LINES_MOST_INTERESTING [list ]
   foreach LINE $MSG_LINES_NO_UNINTERESTING {
      puts $MESSAGES_NO_UNINTERESTING $LINE
      foreach MSG $MOST_INTERESTING {
         if { [regexp $MSG $LINE] } {
            lappend MSG_LINES_MOST_INTERESTING $LINE
            break
         }
      }
   }

   # close the file for all messages not defined in the UNINTERESTING list
   close $MESSAGES_NO_UNINTERESTING

   # open a file for messages defined in the MOST_INTERESTING list
   set MESSAGES_MOST_INTERESTING [open "messages-most_interesting.txt" "w+"]

   # write all most interesting messages to the corresponding file
   foreach LINE $MSG_LINES_MOST_INTERESTING {
      puts $MESSAGES_MOST_INTERESTING $LINE
   }

   # close the file for messages defined in the MOST_INTERESTING list
   close $MESSAGES_MOST_INTERESTING
}
